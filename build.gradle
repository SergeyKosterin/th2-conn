/*
 * Copyright 2020-2020 Exactpro (Exactpro Systems Limited)
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id "de.undercouch.download" version "4.0.4" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id "com.jfrog.artifactory" version '4.15.1'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        sharedDir                       = file("${project.rootDir}/shared")

        connectivityDockerImageVersion  = "${version_major}.${version_minor}.${version_maintenance}.${version_build}"
    }

    group = 'com.exactpro.th2'
    version = "${version_major}.${version_minor}-SNAPSHOT"

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Schema snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/th2-schema-snapshot-local'
        }

        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        mavenLocal()
        

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    artifactoryPublish.skip = true

    artifactory {
        publish {
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = true
                // Properties to be attached to the published artifacts.
                publishPom = true
            }
        }
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.palantir.docker'

ext {
    sailfishVersion                 = '3.2-SNAPSHOT'

    slf4jVersion        = '1.7.30'
    th2CommonVersion    = '1.1-SNAPSHOT'

    cradleVersion       = '1.3-SNAPSHOT'
    cassandra_version   = '3.6.0' //FIXME: Migrate to another library for UUID

    dockerImageVersion  = "${version_major}.${version_minor}.${version_maintenance}.${version_build}"
}

archivesBaseName = 'connectivity-service'

sourceCompatibility = 1.11
targetCompatibility = 1.11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

dependencies {
    compile(project(':grpc-connectivity'))

    compile group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.0.4'

    compile "com.rabbitmq:amqp-client:5.8.0"
    compile "com.datastax.cassandra:cassandra-driver-core:${cassandra_version}"//FIXME: Migrate to another library for UUID
    compile "com.exactpro.cradle:cradle-core:${cradleVersion}" // Required to get max size of batch

    compile "com.exactpro.th2:th2-grpc-event-store:${th2CommonVersion}"

    compile "com.exactpro.th2:th2-common:${th2CommonVersion}"
    compile "com.exactpro.sf:sailfish-core:${sailfishVersion}"

    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation 'junit:junit:4.12'
}

application {
    mainClassName 'com.exactpro.th2.connectivity.MicroserviceMain'
}

applicationName = archivesBaseName

distTar {
    archiveName "${archivesBaseName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    name "${target_docker_repository}/${docke_image_name}:${connectivityDockerImageVersion}"
    tag 'local-version', "${docke_image_name}:${connectivityDockerImageVersion}"

    copySpec.from(tarTree("$buildDir/distributions/${archivesBaseName}.tar"))
}